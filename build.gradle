/*******************************************************************************
 * build script dependencies
 *******************************************************************************/
plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}

apply from: 'https://bintray.com/artifact/download/shemnon/javafx-gradle/8.1.1/javafx.plugin'

/*******************************************************************************
 * build script
 *******************************************************************************/

group = 'com.github.xwgou'
version = '0.2.1'

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://raw.github.com/IMAGINARY/maven-repository/master/" }
}

dependencies {
    compile group: 'de.mfo.jsurf', name: 'jsurf', version: '0.3.0'
    compile group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile group: 'com.belerweb', name: 'pinyin4j', version: '2.5.0'
    compile group: 'org.jfxtras', name: 'jfxtras-labs', version: '8.0-r4'
    compile 'org.slf4j:slf4j-api:1.7.10'
    runtime 'ch.qos.logback:logback-classic:1.0.13'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

jar {
    manifest {
        attributes( "Implementation-Title": project.group,
                   "Implementation-Version": project.version,
                   "Implementation-Vendor": "Xin Wei Gou and Christian Stussak" )
    }
}

license {
    // license header file to use
    license { header = file('LICENSE_HEADER') }

    // replacement string in the header
    ext.description = 'This file is part of NameSurfer.'
    ext.year = '2011-' + Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Xin Wei Gou and Christian Stussak'

    // add support for additional file types
    mapping {
        fxml = 'XML_STYLE'
        jsurf = 'SCRIPT_STYLE'
    }

    strictCheck = true
}

javafx {
    appID 'NameSurfer'
    appName 'NameSurfer'
    category = 'Visualization'
    copyright = 'Copyright (c) 2011-' + Calendar.getInstance().get(Calendar.YEAR) + ' Xin Wei Gou and Christian Stussak'
    description = '''A program to render beautiful algebraic surfaces derived from your name.'''
    vendor = 'Xin Wei Gou and Christian Stussak'

    mainClass = 'com.github.xwgou.namesurfer.NameSurfer'

    icons {
        shortcut = [
                'icon-256.png'
        ]
        volume = shortcut
        setup = 'icon-32.bmp'
    }

    installSystemWide = true
}
